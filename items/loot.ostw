import "../utils/index.ostw";

rule: "[Initialize Wishing Star] XP Increment Amount By Lvl"
{
	// This is the amount we should multiply their current "required xp to level" by
	// in order to level them up once, twice, or thrice
	WishingStarXPLevelGainMultiplier[1] = MaxXPGrowthPerLevel;
	WishingStarXPLevelGainMultiplier[2] = WishingStarXPLevelGainMultiplier[1] + MaxXPGrowthPerLevel^2;
	// WishingStarXPLevelGainMultiplier[3] = WishingStarXPLevelGainMultiplier[2] + MaxXPGrowthPerLevel^3;
	// WishingStarXPLevelGainMultiplier[4] = WishingStarXPLevelGainMultiplier[3] + MaxXPGrowthPerLevel^4;
}

rule: "[Use Item] Experience Potion"
Event.OngoingPlayer
Team.Team2
if (Item_ExperiencePotion > 0)
if (IsButtonHeld(Player, Button.Interact) == true)
if (IsAlive(Player) == true)
if (DistanceBetween(Player, UseExperiencePotionLoc) <= 2.5)
{
	SmallMessage(Player, "Consuming Experience Potion...");
	Wait(1, WaitBehavior.AbortWhenFalse);
	PlayEffect(AllPlayers(Team.Team2), PlayEffect.ExplosionSound, Color.White, Player, 100);
	Pointloot = RandomInteger(EXPToLevelUp * 0.25, EXPToLevelUp * 0.8);
	Item_ExperiencePotion -= 1;
	MinWait();
	NewMessage(<"Gained <0> Experience Points!", Pointloot>);
	EXP += Pointloot;
	Wait(1, WaitBehavior.AbortWhenFalse);
	LoopIfConditionIsTrue();
}

rule: "[Use Item] Money Bag"
Event.OngoingPlayer
Team.Team2
if (IsButtonHeld(Player, Button.Interact) == true)
if (Item_MoneyBag > 0)
if (IsAlive(Player) == true)
if (DistanceBetween(Player, UseMoneyBagLoc) <= 2.5)
{
	SmallMessage(Player, "Opening Money Bag...");
	Wait(1, WaitBehavior.AbortWhenFalse);
	PlayEffect(AllPlayers(Team.Team2), PlayEffect.BuffImpactSound, Color.White, Player, 100);
	Pointloot = RandomInteger(500, 10000);
	Item_MoneyBag -= 1;
	MinWait();
	Coins += Pointloot;
	NewMessage(<"Received <0> Coins from Opening Money Bag", Pointloot>);
	Wait(1, WaitBehavior.AbortWhenFalse);
	LoopIfConditionIsTrue();
}

rule: "[Use Item] Wishing Star"
Event.OngoingPlayer
Team.Team2
if (IsButtonHeld(Player, Button.Interact) == true)
if (Item_WishingStar > 0)
if (IsAlive(Player) == true)
if (DistanceBetween(Player, TossWishingStarLoc) <= 2)
{
	SmallMessage(Player, "Tossing Wishing Star...");
	Wait(1, WaitBehavior.AbortWhenFalse);
	PlayEffect(AllPlayers(Team.Team2), PlayEffect.RingExplosionSound, Color.White, Player, 100);
	PlayEffect(AllPlayers(Team.Team2), PlayEffect.RingExplosion, Color.Violet, Player, 8);
	define randomNum = RandomInteger(1,1);
	Item_WishingStar -= 1;
	MinWait();
	
	if (randomNum == 1) {
		define randomLevels = RandomInteger(1, 2);
		MinWait();
		NewMessage(<"Wishing Star has granted you <0> Free Level(s)", randomLevels>);
		BigMessage(AllPlayers(Team.Team2), <"<0> has received <1> Free Level(s) from Wishing Star!", Player, randomLevels>);
		EXP = EXP + (EXPToLevelUp * WishingStarXPLevelGainMultiplier[randomLevels]);
	}
}