import "../utils/index.ostw";

rule: "[Player] Level Up"
Event.OngoingPlayer
Team.Team2
if (EXP >= EXPToLevelUp)
{
    EXP = EXP - EXPToLevelUp;
    PlayerLevel += 1;
    StatPoints += root.StatPointsPerLevel;
    if (PlayerLevel % root.TraitPointGainedEveryXLevels == 0) {
        TraitPoints += 1;
    }
    EXPToLevelUp *= MaxXPGrowthPerLevel;
    PlayEffect(AllPlayers(Team.Team2), PlayEffect.BuffExplosionSound, Color.White, Player, 100);
	PlayEffect(AllPlayers(Team.Team2), PlayEffect.RingExplosion, Color.Green, Player, 12);
    
    NewMessage(<"Ding! You leveled up to <0>!", PlayerLevel>);
    // Loops if you have >1 level of xp, e.g. from wishing star
    LoopIfConditionIsTrue();
}

rule: "[Player] Catch-Up Enabled"
Event.OngoingPlayer
Team.Team2
if (PlayerLevel <= root.AveragePlayerLevel * 0.85)
if (CatchUpEnabled == false)
{
    EXPGainRate += 2;
    Wait(5, WaitBehavior.AbortWhenFalse);
    CatchUpEnabled = true;
    NewMessage("+200% catch-up EXP boost enabled to help you catch up to higher level players");
}

rule: "[Player] Catch-Up Disabled"
Event.OngoingPlayer
Team.Team2
if (PlayerLevel > root.AveragePlayerLevel * 0.85)
if (CatchUpEnabled == true)
{
    EXPGainRate -= 2;
    Wait(3, WaitBehavior.AbortWhenFalse);
    CatchUpEnabled = false;
    NewMessage("+200% catch-up EXP boost disabled");
}