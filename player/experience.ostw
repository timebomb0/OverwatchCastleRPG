import "../utils/index.ostw";

rule: "[Player] Level Up"
Event.OngoingPlayer
if (!IsDummyBot(Player))
if (EXP >= EXPToLevelUp)
{
    EXP = EXP - EXPToLevelUp;
    PlayerLevel += 1;
    MinWait();
    SetPlayerScore(Player, PlayerLevel);
    StatPoints += root.StatPointsPerLevel + PrestigeLevel;
    if (PlayerLevel % root.TraitPointGainedEveryXLevels == 0) {
        TraitPoints += 1;
    }
    EXPToLevelUp *= MaxXPGrowthPerLevel;
    PlayEffect(AllPlayers(Team.All), PlayEffect.BuffExplosionSound, Color.White, Player, 100);
	PlayEffect(AllPlayers(Team.All), PlayEffect.RingExplosion, Color.Green, Player, 12);
	
    UpdateAveragePlayerLevel();
    NewMessage(<"Ding! You leveled up to <0>!", PlayerLevel>);
    // Loops if you have >1 level of xp, e.g. from wishing star
    LoopIfConditionIsTrue();
}

rule: "[Player] Catch-Up Enabled"
Event.OngoingPlayer
if (!IsDummyBot(Player))
if (PlayerLevel <= root.AveragePlayerLevel * 0.85)
if (CatchUpEnabled == false)
{
    UpdateAveragePlayerLevel();
    Wait(0.1, WaitBehavior.AbortWhenFalse);
    EXPGainRate += 4;
    Wait(5, WaitBehavior.AbortWhenFalse);
    CatchUpEnabled = true;
    NewMessage("+400% catch-up EXP boost enabled to help you catch up to higher level players");
}

rule: "[Player] Catch-Up Disabled"
Event.OngoingPlayer
if (!IsDummyBot(Player))
if (PlayerLevel > root.AveragePlayerLevel * 0.85)
if (CatchUpEnabled == true)
{
    UpdateAveragePlayerLevel();
    Wait(0.1, WaitBehavior.AbortWhenFalse);


    EXPGainRate -= 4;
    Wait(3, WaitBehavior.AbortWhenFalse);
    CatchUpEnabled = false;
    NewMessage("+400% catch-up EXP boost disabled");
}