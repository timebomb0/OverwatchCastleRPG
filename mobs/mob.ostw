import "../utils/index.ostw";

class Mob {
	private define CoinsOnKill;
	private define TrinketOnKill;
	private define EnergyOnKill;
	private define CoreDamage;
	private define SpawnMessage;
	private define DamageDealt;
	private define MaxHealth;
	private define MoveSpeed;
	private define HeroMob;
	private define EXPOnKill;
	private define AutoUltChance;
	private define AutoPrimaryAtkChance;
	private define AutoSecondaryAtkChance;
	private define AutoAbility1Chance;
	private define AutoAbility2Chance;
	private define HoldPrimaryFire;
	private define HoldSecondaryFire;
	public define AttackDuringUlt;
	public define AttackInterval;
	
	public Mob(ref define heroMob, ref define coinsOnKill, ref define trinketOnKill,
				ref define energyOnKill, ref define expOnKill,
				ref define spawnMessage,
				ref define coreDamage, ref define damageDealt, ref define maxHealth, ref define moveSpeed,
				ref define autoUltChance, ref define autoPrimaryAtkChance, ref define autoSecondaryAtkChance,
				ref define autoAbility1Chance, ref define autoAbility2Chance,
				ref define attackDuringUlt, ref define holdPrimaryFire, ref define holdSecondaryFire, ref define attackInterval)
	{
		HeroMob = heroMob;
		CoinsOnKill = coinsOnKill;
		TrinketOnKill = trinketOnKill;
		EnergyOnKill = energyOnKill;
		CoreDamage = coreDamage;
		SpawnMessage = spawnMessage;
		DamageDealt = damageDealt;
		MaxHealth = maxHealth;
		MoveSpeed = moveSpeed;
		AutoUltChance = autoUltChance;
		AutoPrimaryAtkChance = autoPrimaryAtkChance;
		AutoSecondaryAtkChance = autoSecondaryAtkChance;
		AutoAbility1Chance = autoAbility1Chance;
		AutoAbility2Chance = autoAbility2Chance;
		AttackDuringUlt = attackDuringUlt;
		AttackInterval = attackInterval;
		HoldPrimaryFire = holdPrimaryFire;
		HoldSecondaryFire = holdSecondaryFire;
		EXPOnKill = expOnKill;
	}

	public void handleDead()
	{
		root.playerItr[SlotOf(root.Player)] = 0;
		while (root.playerItr[SlotOf(root.Player)] < CountOf(root.PlayerList))
		{
			define aPlayer = root.PlayerList[root.playerItr[SlotOf(root.Player)]];
			MinWait();

			define _chillingMobRewardModifier = aPlayer.ChillModeEnabled ? 0.5 : 1;
			aPlayer._MobVars[0] = RoundToInteger(RandomInteger(EXPOnKill * 0.9, EXPOnKill * 1.1) * aPlayer.EXPGainRate * (1 + (aPlayer.PlayerLevel / 50)), Rounding.Nearest);
			aPlayer._MobVars[1] = RoundToInteger(RandomInteger(CoinsOnKill * 0.8, CoinsOnKill * 1.2) * aPlayer.CoinGainRate  * _chillingMobRewardModifier * root.CoinsOnKillMultiplier, Rounding.Nearest);
			// aPlayer._MobVars[2] = RoundToInteger(root.Item_TrinketModifier * TrinketOnKill * aPlayer.TrinketGainRate * _chillingMobRewardModifier, Rounding.Nearest);
			aPlayer._MobVars[2] = 1;
			MinWait();
			
			aPlayer.EXP += aPlayer._MobVars[0];
			aPlayer.Coins += aPlayer._MobVars[1];
			aPlayer.Item_Trinket += aPlayer._MobVars[2];

			aPlayer.RecentGains[0] += aPlayer._MobVars[0];
			aPlayer.RecentGains[1] += aPlayer._MobVars[1];
			aPlayer.RecentGains[2] += aPlayer._MobVars[2];

			root.playerItr[SlotOf(root.Player)] += 1;
			MinWait();
		}

		PlayEffect(AllPlayers(Team.All), PlayEffect.BadPickupEffect, Color.Red, PositionOf(root.Player), 2);
		root.MobKills += 1;
		if (root.CurrentQuest == 2) {
			root.CurrentQuestKills += 1;
		}
	}

	public void despawn(ref define mob)
	{
		PlayEffect(AllPlayers(Team.All), PlayEffect.ExplosionSound, Color.White, mob, 100);
		if (root.CurrentQuest == 1 && root.CoreHealth > 0) {
			root.CoreHealth -= 1;
		}
		Kill(mob, null);
	}

	public void spawn()
	{
		CreateDummyBot(HeroMob, Team.All, root.CurrentMobSlot, Vector(0, 0, 0), Vector(0, 0, 0));
		MinWait();
    	root.BotList = FilteredArray(AllPlayers(Team.All), IsDummyBot(ArrayElement()));
		MinWait();

		root.UpdateAveragePlayerLevel();
		define mob = PlayersInSlot(root.CurrentMobSlot, Team.All);
		mob.PlayerLevel = root.AveragePlayerLevel;

		StartDamageModification(mob, root.BotList, 0, DamageModificationRev.ReceiversAndDamagers);

		define MobLevelModifier = 1 + (root.AveragePlayerLevel * 0.4);
		SetHealingReceived(PlayersOnHero(HeroMob, Team.Team1), 0);
		SetMaxHealth(mob, MobLevelModifier * root.DifficultyModifier * root.MobHealthModifier * CountOf(root.PlayerList) * MaxHealth);
		MinWait();
		SetPlayerHealth(mob, MaxHealth(mob));
		SetDamageDealt(mob, MobLevelModifier * root.DifficultyModifier * root.MobDamageModifier * DamageDealt);
		SetMoveSpeed(mob, (MobLevelModifier / 2) * root.MobSpeedModifier * MoveSpeed);

		CreateInWorldText(
			AllPlayers(Team.All),
			<"Level <0>", mob.PlayerLevel>,
			mob, 1, Clipping.ClipAgainstSurfaces, InworldTextRev.VisibleToPositionAndString, Color.Red, Spectators.DefaultVisibility);
		mob.MobLevelText = LastTextID();
	}
	
	public void attack(ref define mob)
	{
		root.RandomNumber = RandomInteger(1, 100);
		Wait(0.1, WaitBehavior.IgnoreCondition);
	
		if (AutoUltChance != false && root.RandomNumber <= AutoUltChance && IsUsingUltimate(mob) != true) {
			SetUltimateCharge(mob, 100);
			Wait(0.1, WaitBehavior.IgnoreCondition);
			PressButton(mob, Button.Ultimate);
		}

		if (IsUsingUltimate(mob)) {
			if (HeroOf(mob) == Hero.Mccree || HeroOf(mob) == Hero.Junkrat) {
				Wait(RandomInteger(2, 5), WaitBehavior.AbortWhenFalse);
			} 
			StartHoldingButton(mob, Button.PrimaryFire);
			Wait(2, WaitBehavior.IgnoreCondition);
			StopHoldingButton(mob, Button.PrimaryFire);
			
			if (IsUsingUltimate(mob)) {
				LoopIfConditionIsTrue();
			}
		}

		if (AutoAbility1Chance != false && root.RandomNumber <= AutoAbility1Chance) {
			PressButton(mob, Button.Ability1);
			mob.MobHasAttacked = true;
		}

		if (AutoAbility2Chance != false && root.RandomNumber <= AutoAbility2Chance) {
			if (mob.MobHasAttacked == true) {
				Wait(AttackInterval, WaitBehavior.AbortWhenFalse);
			}
			
			PressButton(mob, Button.Ability2);
			if (HeroMob == Hero.Moira) {
				Wait(1, WaitBehavior.IgnoreCondition);
				PressButton(mob, Button.SecondaryFire);
			}
			mob.MobHasAttacked = true;
		}

		if (AutoPrimaryAtkChance != false && root.RandomNumber <= AutoPrimaryAtkChance) {
			if (mob.MobHasAttacked == true) {
				Wait(AttackInterval, WaitBehavior.AbortWhenFalse);
			}

			if (HoldPrimaryFire){
				StartHoldingButton(mob, Button.PrimaryFire);
				Wait(RandomInteger(0.500, 3), WaitBehavior.IgnoreCondition);
				StopHoldingButton(mob, Button.PrimaryFire);
			} else {
				PressButton(mob, Button.PrimaryFire);
			}
			mob.MobHasAttacked = true;
		}

		if (AutoSecondaryAtkChance != false && root.RandomNumber <= AutoSecondaryAtkChance) {
			if (mob.MobHasAttacked == true) {
				Wait(AttackInterval, WaitBehavior.AbortWhenFalse);
			}

			if (HoldSecondaryFire){
				StartHoldingButton(mob, Button.SecondaryFire);
				Wait(RandomInteger(0.500, 3), WaitBehavior.IgnoreCondition);
				StopHoldingButton(mob, Button.SecondaryFire);
			} else {
				PressButton(mob, Button.SecondaryFire);
			}
		}
		
		mob.MobHasAttacked = false;
	}
}